import "time"

//corgi:formixin Time Date DateTime SetLocation
- location := time.Local

// SetLocation updates the default location used for Time, Date, and DateTime.
mixin SetLocation(loc time.Location)
  - location = loc

//corgi:formixin Time SetTimeFormat
- timeFormat := "15:04"

// Time formats t according to format, which defaults to the global time format,
// changeable through SetTimeFormat.
//
// By default, the time is printed in the global default location, which by
// default is time.Location, changeable trough SetLocation.
//
// If asIs is true, the time won't be converted to another timezone.
mixin Time(t time.Time, format = timeFormat, in = location, asIs = false)
  if !asIs
    - t = t.In(in)

  = t.Format(format)

// SetTimeFormat sets the default global time format.
//
// If left unchanged, it will be "15:04".
mixin SetTimeFormat(format string)
  - timeFormat = format

//corgi:formixin Date SetDateFormat
- dateFormat := "2006-01-02"

// Date formats t according to format, which defaults to the global time format,
// changeable through SetTimeFormat.
//
// Unlike Time and DateTime, the date will be printed in the timezone that t is
// already in.
//
// This can be changed by setting asIs to true, after which, the date is
// printed in the global default location, which by default is time.Location,
// changeable trough SetLocation.
// This is usually only desired, if t is a datetime and we only want to print
// the date.
mixin Date(t time.Time, format = dateFormat, in = location, asIs = true)
  if !asIs
    - t = t.In(in)

  = t.Format(format)

// SetDateFormat sets the default global date format.
//
// If left unchanged, it will be "2006-01-02".
mixin SetDateFormat(format string)
  - dateFormat = format

//corgi:formixin DateTime SetDateTimeFormat
- dateTimeFormat := "2006-01-02 15:04"

// DateTime formats t according to format, which defaults to the global time
// format, changeable through SetDateTimeFormat.
//
// By default, the datetime is printed in the global default location, which by
// default is time.Location, changeable trough SetLocation.
//
// If asIs is true, the time won't be converted to another timezone.
mixin DateTime(t time.Time, format = dateTimeFormat, in = location, asIs = false)
  if !asIs
    - t = t.In(in)

  = t.Format(format)

// SetDateTimeFormat sets the default global date time format.
//
// If left unchanged, it will be "2006-01-02 15:04".
mixin SetDateTimeFormat(format string)
  - dateTimeFormat(format)